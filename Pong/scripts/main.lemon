//	-	Inclusions	-
include framework/?
include pong/?
include define
include menus
include util

//	-	-	-	-	-

// Fun fact. "bg_dataselect does not work for some reason, so I used a new sprite defined as "custom_background".
function void DataSelect.drawCustomPlanes()
{
	// Check for the screen the menu is on.
	if (menuScreen == 1)
	{
		// If you are wondering why I didn't do CustomMenu.drawCustomPlanes() it didn't load quite as fast as I would like it to.

		u8 fadeEffect = 0x55
		if (!Game.Lite)
		{
			fadeEffect += Gameplay.bgGlow
			if ((Gameplay.bgGlow > 0) && (!Game.Paused))
				Gameplay.bgGlow--
		}

		// Render the custom background.
		if (Game.SquidGameSecret)
			Renderer.drawCustomSprite("pong_bg_sg", getScreenExtend() - 40, 0, 0x00, 0, 0x3000, 0, fadeEffect)
		else
			Renderer.drawCustomSprite("pong_bg", getScreenExtend() - 40, 0, 0x00, 0, 0x3000, 0, fadeEffect)
	}
	else
	{
		// Call the base Data Select background script. Useful for mod compatibility with custom Data Select menus.
		base.DataSelect.drawCustomPlanes()
	}
}

function void writeDisplay(u8 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(u16 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(u32 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(u64 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(s8 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(s16 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(s32 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void writeDisplay(s64 display_1)
{
	System.writeDisplayLine(stringformat("%x", display_1))
}

function void Pong.menuHandler(u8 max)
{
	if (Game.SettingMenuLock)
		return
	if (control.pad1.pressed & CONTROL_DOWN)
	{
		playSound(0x5b)
		if (Game.MenuSelect != max)
			Game.MenuSelect++
		else
			Game.MenuSelect = 0
	}
	else if (control.pad1.pressed & CONTROL_UP)
	{
		playSound(0x5b)
		if (Game.MenuSelect != 0)
			Game.MenuSelect--
		else
			Game.MenuSelect = max
	}
}

function void Renderer.pong.drawMenu(u8 count, string text, bool isGray)
{
	u32 color = 0xffffffff
	if (isGray)
		color = 0xaaaaaaff
	else if (Game.MenuSelect == count)
		color = 0xffff00ff
	
	Renderer.drawText("sonic3_fontB", getScreenWidth() / 2, 50 + (count * 12), ((Game.MenuSelect == count) ? stringformat("* %s *", text) : text), color, 2, 0, 0xf100, false)
}

function void Renderer.pong.drawMenuText(u8 count, u8 alignment, string text)
{
	s16 xPos = getScreenWidth() / 2 - 100
	if (alignment == 2)
		xPos = getScreenWidth() / 2
	Renderer.drawText("sonic3_fontB", xPos, 50 + (count * 8), text, 0xffffffff, alignment, 0, 0xf100, false)
}

function void Renderer.pong.drawAchievement(u8 count, string text, bool isGray)
{
	u32 color = 0xffffffff
	if (isGray)
		color = 0xaaaaaaff
	else if (Game.MenuSelect == count)
		color = 0xffff00ff
	Renderer.drawText("sonic3_fontB", getScreenWidth() / 2 - 100, 50 + (count * 8), ((Game.MenuSelect == count) ? stringformat("* %s *", text) : text), color, 1, 0, 0xf100, false)
}

function void Renderer.pong.drawTitle()
{
	if (!Game.Lite)
	{
		s16 posX = (level.framecounter / 2 % 120) - 120
		for (u16 i = 0; i != 6; i++)
			Renderer.drawText("oxyfont_regular", (posX) + (i * 120), 20, "Sonic 3 A.I.R. Pong", 0xffffffff, 2, 0, 0xf100, false)
	}
	else
		Renderer.drawText("oxyfont_regular", getScreenWidth() / 2, 20, "Sonic 3 A.I.R. Pong", 0xffffffff, 2, 0, 0xf100, false)
}

function void Renderer.pong.drawBoostBar(bool isSecondPlayer)
{
	s16 xPos = ((isSecondPlayer) ? getScreenWidth() - 40 : 40)
	s16 yPos = ((isSecondPlayer) ? Pong.P2.Y : Pong.P1.Y)
	for (u16 i = 0; i != Ball.HoldTimer; i++)
		Renderer.drawCustomSprite(((i <= 4) ? "pong_hold_0" : (i <= 10) ? "pong_hold_1" : (i <= 18) ? "pong_hold_2" : "pong_hold_3"), xPos, yPos + 9 - i, 0x00, 0, 0xf000)
}
